group = 'com.hearthintellect'
version = '0.1'

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    springMVCVersion = '4.3.5.RELEASE'
}

dependencies {
    compile (group: 'org.springframework', name: 'spring-webmvc', version: "$springMVCVersion") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile group: 'org.springframework.data', name: 'spring-data-mongodb', version: '1.9.6.RELEASE'

    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.12'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.12'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    compile group: 'org.jsoup', name: 'jsoup', version:'1.9.2'
    compile group: 'org.json', name: 'json', version: '20160810'

    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.4.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'com.github.fakemongo', name: 'fongo', version: '2.0.6'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'
    testCompile group: 'org.springframework', name: 'spring-test', version: "$springMVCVersion"
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.4.0'
}

task downloadHearthJson(type: JavaExec) {
    main = 'com.hearthintellect.crawler.HearthJsonDownloader'
    classpath = sourceSets.main.runtimeClasspath
}

task parseHearthJson(type: JavaExec, dependsOn: downloadHearthJson) {
    main = 'com.hearthintellect.crawler.HearthJsonCardParser'
    classpath = sourceSets.main.runtimeClasspath
}

task crawlHearthHead(type: JavaExec, dependsOn: parseHearthJson) {
    main = 'com.hearthintellect.crawler.HearthHeadCardCrawler'
    classpath = sourceSets.main.runtimeClasspath
}

task downloadAssets(type: JavaExec, dependsOn: crawlHearthHead) {
    main = 'com.hearthintellect.crawler.HearthHeadAssetDownloader'
    classpath = sourceSets.main.runtimeClasspath
}

task prepareData(dependsOn: downloadAssets)

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
